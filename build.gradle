plugins {
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless' version '7.0.4'
}

group 'aw.algorithms'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.1'
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.17.0') // or prettier() or eclipse()
        importOrder 'java', 'javax', 'org', 'com' // optional
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jacoco {
    toolVersion = "0.8.13"  // Latest as of 2025
}

test {
    useJUnitPlatform() // if using JUnit 5
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }

        rule {
            limit {
                counter = 'LINE'
                minimum = 0.9
            }
        }
    }
}